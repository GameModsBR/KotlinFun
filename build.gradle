allprojects {
    group 'br.com.gamemods.kotlinfun'
    version '0.2-SNAPSHOT'

    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

buildscript {
    ext.kotlin_version = '1.0.3'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    dependencies {
        compile rootProject
    }

    shadowJar {
        dependencies {
            include dependency("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
            include dependency("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version")
            include dependency("org.jetbrains.kotlin:kotlin-runtime:$kotlin_version")
            include dependency(rootProject)
        }

        classifier = null
        baseName = rootProject.name+'-'+project.name
    }

    build.finalizedBy shadowJar

    task copyFinalJar(type: Copy) {
        from "${project.buildDir}/libs/${shadowJar.archiveName}"
        into "${rootProject.buildDir}"
    }

    shadowJar.finalizedBy(copyFinalJar)
}

project(':BungeeChat') {
    dependencies {
        compile 'net.md-5:bungeecord-chat:1.10-SNAPSHOT'
    }

    shadowJar.enabled = false
}

project(':UniversalPlugin') {
    dependencies {
        compile project(':BungeePlugin')
        compile project(':SpigotPlugin')
    }

    shadowJar {
        dependencies {
            include project(':BungeePlugin')
            include project(':BukkitPlugin')
            include project(':SpigotPlugin')
        }
    }
}
