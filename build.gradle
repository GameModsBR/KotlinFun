allprojects {
    group 'br.com.gamemods.kotlinfun'
    version '0.1'
}

buildscript {
    ext.kotlin_version = '1.0.3'
    ext.bukkit_repo = 'https://hub.spigotmc.org/nexus/content/groups/public/'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'kotlin'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
        compile rootProject
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    /*
    jar {
        baseName = "KotlinFun-${project.name}"
    }
    */

    shadowJar {
        dependencies {
            include dependency("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
            include dependency("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version")
            include dependency("org.jetbrains.kotlin:kotlin-runtime:$kotlin_version")
            include dependency(rootProject)
        }

        classifier = null
        // baseName = "KotlinFun-${project.name}"
        // relocate 'kotlin', 'br.com.gamemods.kotlinfun.kotlin'
    }

    build.finalizedBy shadowJar
}

project(':BungeeChat') {
    dependencies {
        compile 'net.md-5:bungeecord-chat:1.10-SNAPSHOT'
    }
}

project(':UniversalPlugin') {
    repositories {
        maven { url = bukkit_repo }
    }

    dependencies {
        compile project(':BungeePlugin')
        compile project(':BukkitPlugin')
    }

    shadowJar {
        dependencies {
            include project(':BungeePlugin')
            include project(':BukkitPlugin')
        }
    }
}
